' {$STAMP BS2p}
' {$PBASIC 2.5}

MotR        CON        8           'Serial out pin for right motor
MotL        CON        9           'Serial out pin for left motor
Bmode       CON        16385       'Constant for 115,200 baud, 8 bit, no parity, inverted
DriveDelay  CON        6           'Delay to scale start routine times



' --------------Variables------------------------
'
Pattern     VAR        Byte        'The Start pattern to be run
SearchBit   VAR        Bit         'Start pattern search bit - 0 if search to the left, 1 if search to the right
SideBit     VAR        Bit         'Side that opponent was last seen on (0 if front, 1 if back
IRoutFront  VAR        Byte        'Front Sensor output to Branch
IRoutBack   VAR        Byte        'Back Sensor output to Branch
NewSpdRt    VAR        Byte        'Right motor speed 0 is full reverse, 255 is full forward, 127 and 128 are off
NewSpdLeft  VAR        Byte        'Left motor speed 0 is full reverse, 255 is full forward, 127 and 128 are off
Delay       VAR        Byte        'Counter

HitCount_F  VAR        Byte        'Hit Counter for Front Sensors
HitCount_B  VAR        Byte        'Hit Counter for Back Sensors

HitMax      CON        8           'Number of hits recived on opposite side before direction will be changed

' --------------Initializations------------------
'
MAINIO
'turn off IR LEDS
LOW 1
LOW 2
LOW 3
LOW 4
LOW 5

LOW 10
LOW 11
LOW 12
LOW 13
LOW 14

AUXIO
INPUT 0
INPUT 1
INPUT 2
INPUT 3

'initialize variables
NewSpdRt = 127
NewSpdLeft = 127
Pattern = 0
Delay = 0
IRoutFront = 0
IRoutBack = 0

'**************************************************************************
'**************************************************************************
'**************************************************************************
'***************Main Program***********************************************


Main:
GOSUB Sensors
GOSUB Branching
GOSUB Drive
GOSUB Kill
PAUSE 10
GOTO Main


'**************************************************************************
'**************************************************************************
'*********Motor Control****************************************************

Drive:
'uncomment if Display routine is removed
'PAUSE DriveDelay

'Displays debug information on Serial LCD
AUXIO
SEROUT 15,2,[27,115]
SEROUT 15,2,[27,76,0,0,"Rt:   ",DEC NewSpdRt,   "   "]
SEROUT 15,2,[27,76,0,1,"Left: ",DEC NewSpdLeft, "   "]

SEROUT 15,2,[27,76,10,0,"Side:   ", BIN1 SideBit]
SEROUT 15,2,[27,76,10,1,"Search: ", BIN1 SearchBit]

SEROUT 15,2,[27,76,0,2,"IRFront: ",BIN5 IROutFront]
SEROUT 15,2,[27,76,0,3,"IRBack:  ",BIN5 IROutBack]

SEROUT 15,2,[27,76,16,2,"F ",DEC HitCount_F]
SEROUT 15,2,[27,76,16,3,"B ",DEC HitCount_B]

MAINIO
SEROUT MotR, Bmode, ["!G ",SDEC ((NewSpdRt-127)<<3),CR]
SEROUT MotL, Bmode, ["!G ",SDEC ((127-NewSpdLeft)<<3),CR]

RETURN


'**************************************************************************
'**************************************************************************
'*************Check Sensors************************************************

Sensors:
MAINIO

'Front Plow Sensors
IROutFront = 0

FREQOUT 1, 2, 10407   'Right Front IR sensor
IROutFront.BIT4 = IN6
FREQOUT 2, 2, 10303   'Right Center Front IR sensor
IROutFront.BIT3 = IN6
FREQOUT 3, 2, 10812   'Center Front IR sensor
IROutFront.BIT2 = IN6
FREQOUT 4, 2, 17610   'Left Center Front IR sensor
IROutFront.BIT1 = IN6
FREQOUT 5, 2, 10725   'Left Front IR sensor
IROutFront.BIT0 = IN6

'Back Plow Sensors
IROutBack = 0

FREQOUT 10, 2, 10407   'Right Back IR sensor
IROutBack.BIT4 = IN15
FREQOUT 11, 2, 10303   'Right Center Back IR sensor
IROutBack.BIT3 = IN15
FREQOUT 12, 2, 10812   'Center Back IR sensor
IROutBack.BIT2 = IN15
FREQOUT 13, 2, 17610   'Left Center Back IR sensor
IROutBack.BIT1 = IN15
FREQOUT 14, 2, 10725   'Left Back IR sensor
IROutBack.BIT0 = IN15

'Uncomment for debugging purposes, will blind the sensors
'IRoutFront = 31
'IRoutBack = 31

RETURN


'**************************************************************************
'**************************************************************************
'***************Branching**************************************************

Branching:
'detect which side has more hits

'if Side = Front
IF (SideBit = 0) THEN
 'if there are hits on the opposite plow alone, increase hit counter
 IF (IRoutFront = 31) AND (IRoutBack < 31) THEN
   HitCount_F = 0
   HitCount_B = HitCount_B + 1
 ELSE
   'Otherwise, zero the counters
   HitCount_F = 0
   HitCount_B = 0
 ENDIF
ENDIF

'if Side = Back
IF (SideBit = 1) THEN
 'if there are hits on the opposite plow alone, increase hit counter
 IF (IRoutBack = 31) AND (IRoutFront < 31) THEN
   HitCount_B = 0
   HitCount_F = HitCount_F + 1
 ELSE
   'Otherwise, zero the counters
   HitCount_B = 0
   HitCount_F = 0
 ENDIF
ENDIF

'if Hitcount exceeds maximum, change directions
IF HitCount_F > HitMax THEN
 SearchBit = SearchBit ^ 1 'make sure to change search direction
 SideBit=0
 HitCount_B = 0
 HitCount_F = 0
 'Write new direction bit to EEPROM, at address #0
 WRITE 0, SideBit

ENDIF

'if Hitcount exceeds maximum, change directions
IF HitCount_B > HitMax THEN
 SearchBit = SearchBit ^ 1 'make sure to change search direction
 SideBit=1
 HitCount_B = 0
 HitCount_F = 0

 'Write new direction bit to EEPROM, at address #0
 WRITE 0, SideBit
ENDIF


IF (SideBit = 0) THEN GOSUB BranchingFront
IF (SideBit = 1) THEN GOSUB BranchingBack

RETURN



'************Front Branching Routine***************************************
BranchingFront:
BRANCH IRoutFront, [F_Straight,F_EasyLeft,F_Left, F_Left, F_Straight,F_HardLeft,F_HardLeft,F_HardLeft,F_Right,F_EasyRight,F_Search,F_SearchLeft,F_HardRight,F_Search,F_Search,F_SlowLeft,F_EasyRight,F_Straight,F_EasyLeft,F_EasyLeft,F_HardRight,F_Search,F_Search,F_Left, F_Right,F_EasyRight,F_SearchRight,F_StraightR,F_HardRight,F_Right,F_SlowRight,F_Search]
'0 = hit            00000      00001      00010   00011   00100      00101       00110     00111      01000   01001       01010    01011        01100       01101    01110    01111      10000       10001      10010      10011      10100       10101    10110    10111   11000   11001       11010         11011       11100       11101   11110       11111

'************Back Branching Routine****************************************
BranchingBack:
BRANCH IRoutBack, [B_Straight,B_EasyLeft,B_Left, B_Left, B_Straight,B_HardLeft,B_HardLeft,B_HardLeft,B_Right,B_EasyRight,B_Search,B_SearchLeft,B_HardRight,B_Search,B_Search,B_SlowLeft,B_EasyRight,B_Straight,B_EasyLeft,B_EasyLeft,B_HardRight,B_Search,B_Search,B_Left, B_Right,B_EasyRight,B_SearchRight,B_StraightR,B_HardRight,B_Right,B_SlowRight,B_Search]
'0 = hit           00000      00001      00010   00011   00100      00101      00110      00111      01000   01001       01010    01011        01100       01101    01110    01111      10000       10001      10010      10011      10100       10101    10110    10111   11000   11001       11010         11011       11100       11101   11110       11111



'************Motor Routines************************************************
F_StraightR:        '****************Straight, change search direction
SearchBit = SearchBit ^ 1
GOTO F_Straight


F_SearchRight:      '****************Search to right
SearchBit = 1
GOTO F_Search


F_SearchLeft:       '****************Search to left
SearchBit = 0
GOTO F_Search


'FIXME!!! TURNS WAY TO WIDE
F_Search:           '****************Search to right if SearchBit = 1, Search to left if SearchBit = 0
NewSpdLeft  = 150+(SearchBit*50)
NewSpdRt = 200-(SearchBit*50)
RETURN


F_Straight:         '****************Straight****************
NewSpdLeft = 255
NewSpdRt = 255
RETURN


F_HardRight:        '****************Hard right****************
SearchBit = 1
NewSpdLeft  = 255
NewSpdRt = 135
RETURN


F_HardLeft:         '****************Hard left****************
SearchBit = 0
NewSpdLeft  = 135
NewSpdRt = 255
RETURN


F_Right:            '****************Right****************
SearchBit = 1
NewSpdLeft  = 255
NewSpdRt = 160
RETURN


F_Left:             '****************Left****************
SearchBit = 0
NewSpdLeft  = 160
NewSpdRt = 255
RETURN


F_EasyRight:        '****************Easy right****************
SearchBit = 1
NewSpdLeft  = 255
NewSpdRt = 185
RETURN


F_EasyLeft:         '****************Easy left****************
SearchBit = 0
NewSpdLeft  = 185
NewSpdRt = 255
RETURN


F_SlowRight:        '****************Slow right****************
SearchBit = 1
NewSpdLeft  = 255
NewSpdRt = 100
RETURN


F_SlowLeft:         '****************Slow left****************
SearchBit = 0
NewSpdLeft  = 100
NewSpdRt = 255
RETURN

'***************************Backward Motor Routines************

B_StraightR:        '****************Straight, change search direction
SearchBit = SearchBit ^ 1
GOTO B_Straight


B_SearchRight:      '****************Search to right
SearchBit = 1
GOTO B_Search


B_SearchLeft:       '****************Search to left
SearchBit = 0
GOTO B_Search

'FIXME!!! TURNS WAY TO WIDE
B_Search:           '****************Search to right if SearchBit = 1, Search to left if SearchBit = 0
NewSpdLeft  = 55+(SearchBit*50)
NewSpdRt = 105-(SearchBit*50)
RETURN


B_Straight:         '****************Straight****************
NewSpdLeft = 0
NewSpdRt = 0
RETURN


B_HardRight:        '****************Hard right****************
SearchBit = 1
NewSpdLeft  = 120
NewSpdRt = 0
RETURN


B_HardLeft:         '****************Hard left****************
SearchBit = 0
NewSpdLeft  = 0
NewSpdRt = 120
RETURN


B_Right:            '****************Right****************
SearchBit = 1
NewSpdLeft  = 255
NewSpdRt = 160
RETURN


B_Left:             '****************Left****************
SearchBit = 0
NewSpdLeft  = 0
NewSpdRt = 95
RETURN


B_EasyRight:        '****************Easy right****************
SearchBit = 1
NewSpdLeft  = 70
NewSpdRt = 0
RETURN


B_EasyLeft:         '****************Easy left****************
SearchBit = 0
NewSpdLeft  = 0
NewSpdRt = 70
RETURN


B_SlowRight:        '****************Slow right****************
SearchBit = 1
NewSpdLeft  = 155
NewSpdRt = 0
RETURN


B_SlowLeft:         '****************Slow left****************
SearchBit = 0
NewSpdLeft  = 0
NewSpdRt = 155
RETURN




'**************************************************************
'*****************************Kill*****************************
'**************************************************************

Kill:
       AUXIO
       IF (IN0 = 1) THEN GOTO Redlight
       IF (IN1 = 1) THEN GOTO Redlight
       IF (IN2 = 1) THEN GOTO Redlight
       IF (IN3 = 1) THEN GOTO Redlight


       IF (IN14 = 0) THEN
        PAUSE 20
        IF (IN14 = 0) THEN
          GOTO Redlight
        ENDIF
       ENDIF

       RETURN

Redlight:
          MAINIO
          SEROUT MotR, Bmode, ["!G ",SDEC ((0)<<3),CR]
          SEROUT MotL, Bmode, ["!G ",SDEC ((0)<<3),CR]

          AUXIO
          SEROUT 15,2,[27,115]
          SEROUT 15,2,[27,76,5,0,"DEAD DEAD"]
          SEROUT 15,2,[27,76,7,1,BIN5 IROutFront]
          SEROUT 15,2,[27,76,7,2,BIN5 IROutBack]
          LOW 14
          PAUSE 100
          GOTO redlight